<!--
Copyright 2006-2010 Kevin Kazmierczak. All rights reserved.
Copyright 2010 Dynamo Business Intelligence Corporation. All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

  1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
  2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

  THIS SOFTWARE IS PROVIDED BY Dynamo Business Intelligence Corporation ``AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL Dynamo Business Intelligence Corporation OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
-->
<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
	close="closeWin(event);" 
	keyUp="keyHandler(event);"
	creationComplete="init();" 
	layout="absolute" width="400" height="300">
	<mx:Script>
		<![CDATA[
			import com.flexsqladmin.sqladmin.events.InsertRowEvent;
			import com.adobe.cairngorm.control.CairngormEventDispatcher;
			import mx.controls.TextInput;
			import mx.controls.Button;
			import mx.containers.GridItem;
			import mx.containers.GridRow;
			import mx.containers.HBox;
			import mx.controls.Text;
			import mx.controls.Label;
			import mx.controls.Alert;
        	import mx.managers.PopUpManager;
        	import com.flexsqladmin.sqladmin.vo.OpenTableData;
        	import com.flexsqladmin.sqladmin.components.DebugWindow;
		
			private var tablemetadata:OpenTableData;	
			private var tablexml:XML;
			private var formdata:Array = new Array();
			private var tablewindow:OpenTableWindow;
		
			public function init():void{
				PopUpManager.centerPopUp(this);
			}
			public function closeWin(e:Event):void{
        		PopUpManager.removePopUp(this);
        	}
        	public function setMetaData(metadata:OpenTableData):void{
        		tablemetadata = metadata;
        		this.title = "Insert Into Table: " + tablemetadata.getTable();
        		tablexml = tablemetadata.getTableXML();
        		buildForm();
        	}
        	public function getFormData():Array{
        		return formdata;
        	}
        	public function buildForm():void{
				DebugWindow.log("Building Form");
        		for each(var item:XML in tablexml.NewDataSet.Table){
					DebugWindow.log(item.toString());
        			var formRow:GridRow = new GridRow();
        			var formItem:GridItem = new GridItem();
        			var l:Label = new Label(); 
        			l.text = item.COLUMN_NAME + " (" + item.DATATYPE;
        			if (item.CHARACTER_MAXIMUM_LENGTH != "0")
        				l.text += ", " + item.CHARACTER_MAXIMUM_LENGTH;
        			l.text += ") :";
        			formItem.addChild(l);
        			
        			var formItem2:GridItem = new GridItem();
        			var ti:TextInput = new TextInput();
        			ti.id = "col_" + item.column_name;
        			formdata.push(ti);
					formItem2.addChild(ti);
					
					formRow.addChild(formItem);
					formRow.addChild(formItem2);
					
        			form.addChild(formRow);
        		}
        		var endRow:GridRow = new GridRow();
        		var endItem:GridItem = new GridItem();
        		var b:Button = new Button();
        		b.label = "Insert Record";
        		b.addEventListener(MouseEvent.CLICK, insertClick);
        		endItem.addChild(b);
        		endRow.addChild(endItem);
        		form.addChild(endRow);
        	}
        	private function insertClick(event:Event):void{
        		DebugWindow.log("InsertRowWindow.mxml:insertClick()");
        		var insertrowevent:InsertRowEvent = new InsertRowEvent(tablemetadata, this);
            	CairngormEventDispatcher.getInstance().dispatchEvent(insertrowevent);
        	}
        	private function keyHandler(e:KeyboardEvent):void{
               	// Submit form on enter
                if(e.keyCode == 13)
                	insertClick(new Event(""));
            }
            public function setParentWindow(win:OpenTableWindow):void{
            	tablewindow = win;
            }
            public function getParentWindow():OpenTableWindow{
            	return tablewindow;
            }
		]]>
	</mx:Script>
	<mx:Grid id="form"></mx:Grid>
</mx:TitleWindow>
